class Terrain {
  field WichmannHill rng;
  field int baseHt;
  field Array heights;
  field boolean rtnTrip;

  constructor Terrain new(int seed) {
    let heights = Array.new(17);
    let rng = WichmannHill.new(seed, seed / 2, seed / 3);
    let baseHt = 200;
    let heights[2] = baseHt;
    let heights[3] = baseHt;
    let rtnTrip = false;
    do rng.setMax(100);
    do newHeights();
    return this;
  }

  method void newHeights() {
    var int n;
    let n = 0;
    while (n < 17) {
      if ((~(rtnTrip) & ~(n=2) & ~(n=3)) | ((rtnTrip) & ~(n=12) & ~(n=13))) {
        if ((n = 3) | (n = 14)) {
          let heights[n] = heights[n - 1];
        } else {
          let heights[n] = baseHt - rng.next();
          if (heights[n] < 5) {
            let heights[n] = 5;
          } else {
            if (heights[n] > 250) { let heights[n] = 250; }
          }
        }
      }
      let n = n + 1;
    }
    let rtnTrip = ~rtnTrip;
    return;
  }

  method void draw() {
    var int i;
    let i = 0;
    do Screen.setColor(false);
    while (i < 16) {
      if (~(i = 2) & ~(i = 13)) {
        do Screen.drawLine(i * 32, heights[i], ((i + 1) * 32) - 1, heights[i + 1]);
      }
      let i = i + 1;
    }
    do drawLandings();
    return;
  }

  method void drawLandings() {
    do Screen.drawLine(64, heights[2], 96, heights[2]);
    do Screen.drawLine(64, heights[2] + 10, 96, heights[2]);
    do Screen.drawLine(64, heights[2], 96, heights[2] + 10);
    do Screen.drawLine(416, heights[13], 448, heights[13]);
    do Screen.drawLine(416, heights[13] + 10, 448, heights[13]);
    do Screen.drawLine(416, heights[13], 448, heights[13] + 10);
    return;
  }

  method int getStartHeight() {
    return heights[2];
  }

  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }
}
