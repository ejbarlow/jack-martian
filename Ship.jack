class Ship {

  field int x, y;       // screen position
  field int vX, vY;     // velocities
  field int size;       // square width/height
  field int grav;       // gravity strength
  field int thrust;     // thrust strength
  field int friction;   // horizontal friction strength
  field Array bitMask;  // for checking screen px for hit detection
  field int fuel;
  field bool hitFlag;
  field int thrustDir;
  field bool thrustLen;
  field WichmannHill rng;
  field LunarGame game;

  constructor Ship new(int startY, LunarGame g) {
    var int i;

    let x = 80;
    let y = startY - 4;
    let size = 8;
    let grav = 3;
    let thrust = 8;
    let friction = 1;
    let i = 0;
    let hitFlag = false;
    let rng = WichmannHill.new(23, 7666, 489);
    let game = g;
    let fuel = 200;
    do rng.setMax(2);

    let bitMask = Array.new(16);
    let bitMask[0] = 1;
    while (i < 15) {
      let bitMask[i+1] = bitMask[i] * 2;
      let i = i + 1;
    }

    return this;
  }

  method void update(char key) {
    let thrustDir = 0;
    if (key = 131) {
      if (fuel > 0) {
        let thrustDir = 2;
        let vY = vY - thrust;
        if (vY < -100) { let vY = -100; }
        let fuel = fuel - 1;
      }
    } else {
      let vY = vY + grav;
      if (vY > 100) { let vY = 100; }
    }

    if (key = 130) {
      if (fuel > 0) {
        let thrustDir = 1;
        let vX = vX - thrust;
        if (vX < -100) { let vX = -100; }
        let fuel = fuel - 1;
      }
    } else {
      if (key = 132) { 
        if (fuel > 0) {
          let thrustDir = 3;
          let vX = vX + thrust;
          if (vX > 100) { let vX = 100; }
          let fuel = fuel - 1;
        }
      } else {
        if (vX < 0) {
          let vX = vX + friction;
        } 
        if (vX > 0) {
          let vX = vX - friction;
        }
      }
    }

    let y = y + (vY/20);
    let x = x + (vX/20);

    if (x < 4) {
      let x = 4;
      let vX = 0;
    }
    if (x > 507) {
      let x = 507;
      let vX = 0;
    }
    if (y < 4) {
      let y = 4;
      let vY = 0;
    }

    do hitTest();

    return;
  }

  method int getFuel() {
    return fuel;
  }
   method void addFuel(int amount) {
     let fuel = fuel + amount;
     if (fuel > 200) {
       let fuel = 200;
     }
     return;
   }

  method void drawThrust(int dir) {
    var int tX, tY;
    var int len;
    var int rand;
    let tX = x;
    let tY = y;
    if (thrustLen) {
      let len = 8;
    } else {
      let len = 10;
    }
    let thrustLen = ~thrustLen;
    let rand = rng.next();
    do Screen.setColor(false);
    if (thrustDir = 1) {
      let tX = x + len;
      if (tX > 507) { let tX = 507; }
      do Screen.drawLine(x, y, tX, tY + rand);
      do Screen.drawLine(x, y, tX, tY - rand);
    }
    if (thrustDir = 2) {
      let tY = y + len;
      do Screen.drawLine(x, y, tX + rand, tY);
      do Screen.drawLine(x, y, tX - rand, tY);
    }
    if (thrustDir = 3) {
      let tX = x - len;
      if (tX < 0) { let tX = 0; }
      do Screen.drawLine(x, y, tX, tY + rand);
      do Screen.drawLine(x, y, tX, tY - rand);
    }
    return;
  }

  method bool getHitFlag() {
    return hitFlag;
  }

  method void reset(int spawnY) {
      let x = 80;
      let y = spawnY - 4;
      let vX = 0;
      let vY = 0;
      let hitFlag = false;
      let fuel = 200;
      return;
  }

  method bool hitTest() {
    var int x1, x2, y1;
    let x1 = x - (size/2);
    let x2 = x + (size/2);
    let y1 = y + (size/2);
    while (x1 < x2) {
      if (getPixel(x1, y1) = true) {
        if (~((x > 64) & (x < 96)) & ~((x > 416) & (x < 448))) {
          let hitFlag = true;
          return true;
        } else {
          if (vY > 50) {
            let hitFlag = true;
            return true;
          } else {
            if (vY > 0) {
              let vY = 0;
              let vX = 0;
              do game.score();
            }
          }
        }
      }
      let x1 = x1 + 1;
    }
    let hitFlag = false;
    return false;
  }

  method int getVY() {
    return vY;
  }

  method int getX() {
    return x;
  }

  method bool getPixel(int pX, int pY) {
    var int addr;
    var int word;
    var int m;
    var bool result;

    let result = true;
    let addr = 16384 + (32*pY) + (pX/16);
    let word = Memory.peek(addr);
    let word = ~word;
    let m = pX / 16;
    let m = pX - (m*16);
    if (word & bitMask[m] = 0) {
      let result = false;
    }
    return result;
  }

  method void draw(bool col) {
    do Screen.setColor(col);
    do Screen.drawCircle(x, y, size/2);

    if (thrustDir > 0) {
      do drawThrust(thrustDir);
    }
    do Screen.setColor(~col);
    do Screen.drawCircle(x, y, size/4);
    do Screen.setColor(col);
    return;
  }

  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }
}
