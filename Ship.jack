class Ship {

  field int x, y;       // screen position
  field int vX, vY;     // velocities
  field int size;       // square width/height
  field int grav;       // gravity strength
  field int thrust;     // thrust strength
  field int friction;   // horizontal friction strength
  field Array bitMask;  // for checking screen px for hit detection
  field bool hitFlag;
  field int thrustDir;
  field bool thrustLen;

  constructor Ship new() {
    var int i;

    let x = 256;
    let y = 10;
    let size = 8;
    let grav = 3;
    let thrust = 5;
    let friction = 1;
    let i = 0;
    let hitFlag = false;

    let bitMask = Array.new(16);
    let bitMask[0] = 1;
    while (i < 15) {
      let bitMask[i+1] = bitMask[i] * 2;
      let i = i + 1;
    }

    return this;
  }

  method void update(char key) {
    let thrustDir = 0;
    if (key = 131) {
      let thrustDir = 2;
      let vY = vY - thrust;
      if (vY < -100) { let vY = -100; }
    } else {
      let vY = vY + grav;
      if (vY > 100) { let vY = 100; }
    }

    if (key = 130) {
      let thrustDir = 1;
      let vX = vX - thrust;
      if (vX < -100) { let vX = -100; }
    } else {
      if (key = 132) { 
        let thrustDir = 3;
        let vX = vX + thrust;
        if (vX > 100) { let vX = 100; }
      } else {
        if (vX < 0) {
          let vX = vX + friction;
        } 
        if (vX > 0) {
          let vX = vX - friction;
        }
      }
    }

    let y = y + (vY/20);
    let x = x + (vX/20);

    do hitTest();

    return;
  }

  method void drawThrust(int dir) {
    var int tX, tY;
    var int len;
    let tX = x;
    let tY = y;
    if (thrustLen) {
      let len = 13;
    } else {
      let len = 10;
    }
    let thrustLen = ~thrustLen;
    if (thrustDir = 1) { let tX = x + len; }
    if (thrustDir = 2) { let tY = y + len; }
    if (thrustDir = 3) { let tX = x - len; }
    do Screen.setColor(false);
    do Screen.drawLine(x, y, tX, tY);
    return;
  }

  method bool getHitFlag() {
    return hitFlag;
  }

  method void reset() {
      let y = 10;
      let hitFlag = false;
      return;
  }

  method bool hitTest() {
    var int x1, x2, y1;
    let x1 = x - (size/2);
    let x2 = x + (size/2);
    let y1 = y + (size/2);
    while (x1 < x2) {
      if (getPixel(x1, y1) = true) {
        let hitFlag = true;
        return true;
      }
      let x1 = x1 + 1;
    }
    if (y > 194) {
      if ((x > 64 & x < 96) | (x > 416 & x < 448)) {
        if (vY > 50) {
          let hitFlag = true;
          return true;
        } else {
          let vY = 0;
          let y = 194;
        }
      }
    }
    return false;
  }

  method int getVY() {
    return vY;
  }

  method bool getPixel(int pX, int pY) {
    var int addr;
    var int word;
    var int m;
    var bool result;

    let result = true;
    let addr = 16384 + (32*pY) + (pX/16);
    let word = Memory.peek(addr);
    let word = ~word;
    let m = pX / 16;
    let m = pX - (m*16);
    if (word & bitMask[m] = 0) {
      let result = false;
    }
    return result;
  }

  method void draw(bool col) {
    do Screen.setColor(col);
    do Screen.drawCircle(x, y, size/2);

    if (thrustDir > 0) {
      do drawThrust(thrustDir);
    }
    return;
  }

  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }
}
